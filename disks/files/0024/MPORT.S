*TITLE 	'TEST MEMOIRE PORTAL   *** 01/12/80 ***'
;
;
;
;
;	PROGRAMME DE TEST MEMOIRE PORTAL
;
;
;
;
;	LE TEST DE LA MEMOIRE SE DEROULE EN QUATRE PARTIES.
;	POUR CELA LA MEMOIRE A ETE DECOUPEE EN QUATRE BANCS:
;		-BANC 1 : /4000 A /7FFF
;		-BANC 2 : /8000 A /BFFF
;		-BANC 3 : /C000 A /FFFF
;		-BANC 0 : /0000 A /3BFF + /3C00 A /3FFF
;
;
;
;	LE DERNIER KILO MEMOIRE DU BANC 0 SERA CONSIDERE
;	FONCTIONNER IL CONTIENDRA LES ZONES DE TRAVAIL,
;	LES MESSAGES , LE PROGRAMME , ETC ...
;
;
;	IL Y A DEUX MODES DE FONCTIONNEMENT :
;
;		-UNITAIRE : APRES EXECUTION RENVOI AU CHARGEUR
;		-AUTOMATIQUE : BOUCLAGE INFINI SUR LE TEST
;
;
*EJECT
;
;	LISTE DES EQUIVALENCES
;
AAFF	EQU	09FH	;ADRESSE DU 1ER AFFICHEUR NO 00
VIPR	EQU 	013H	;PARAMETRE D INITIALISATION DU PRINTER
ADEC	EQU	011H	;ADRESSE CLAVIER
ADSC	EQU	010H	;ADRESSE PRINTER + ADRESSE STATUS
LB1	EQU	04000H	;LONGUEUR BANC 1,2 ET 3
LB2	EQU	03C00H	;LONGUEUR BANC 0
AB1L	EQU	04000H	;@ BANC 1 LOWER
AB1U	EQU	07FFFH	;@ BANC 1 UPPER
AB2L	EQU	08000H	;@ BANC 2 LOWER
AB2U	EQU	0BFFFH	;@ BANC 2 UPPER
AB3L	EQU	0C000H	;@ BANC 3 LOWER
AB3U    EQU	0FFFFH	;@ BANC 3 UPPER
AB0L	EQU	00000H	;@ BANC 0 LOWER
AB0U	EQU	03BFFH	;@ BANC 0 UPPER
ETX	EQU	03H	;FIN DE MESSAGE
STAK	EQU	03FFFH	;@ STACK POINTEUR
MEMO	EQU	0	;@ RETOUR EN MEMOIRE MORTE
SIM	EQU 	030H	;INSTRUCTION SIM
AA0	EQU 	080H	;@ AFFICHEUR NO 31
AA1	EQU 	081H	;@ AFFICHEUR NO 30
AA2	EQU 	082H	;@ AFFICHEUR NO 29
ACT1	EQU	084H	;@ AFFICHEUR NO 27
ACT2	EQU	085H	;@ AFFICHEUR NO 26
ACT3	EQU	086H	;@ AFFICHEUR NO 25
ACT4	EQU	087H	;@ AFFICHEUR NO 27
*EJECT
;
;	SOUS-PROGRAMME DE TEST MOSTEK
;
	ORG	03C00H
MOSTEK	EQU	$
	XOR	A
	LD	(MERR),A
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	B,0	;PATTERN INCREMENTE
;
;	PHASE INCREMENTEE
;
MOST1	POP	HL
	POP	DE
	PUSH	DE
	PUSH	HL
	LD	A,'+'
	CALL	NUPA
;
;	CHARGEMENT DE LA ZONE A TESTER
;
MOST2	LD	A,L	;
	XOR	H
	XOR	B
	LD	(HL),A	;PATTERN CALCULE A PARTIR DE L'ADRESSE
	INC	HL
	DEC	DE
	LD	A,D
	OR	E
TRSL1	JP	NZ,MOST2
	POP	HL
	POP	DE
	PUSH	DE
	PUSH	HL
;
;	VERIFICATION DE LA ZONE A TESTER
;
MOST3	LD	A,L
	XOR	H
	XOR	B
	LD	C,A
	LD	A,(HL)	;LECTURE DU PATTERN
	CP	C
	CALL	NZ,ERR?	;SI A # C
	INC	HL
	DEC	DE
	LD	A,E
	OR	D
TRSL2	JP	NZ,MOST3
	INC	B
TRSL3	JP	NZ,MOST1	;CONTINUATION AVEC LE
;				MODIFICATEUR DE PATTERN
;				INCREMENTE
;
;	PHASE DECREMENTEE
;
	POP	HL
	POP	DE
	POP	BC
	LD	H,B
	LD	L,C	;ECHANGE DE BC PAR HL
	PUSH	DE
	PUSH	HL
	LD	B,0
;
;	CHARGEMENT DE LA ZONE A TESTER
;
MOST4	POP	HL
	POP	DE
	PUSH	DE
	PUSH	HL
	LD	A,'-'
	CALL	NUPA
;
MOST5	LD	A,L
	XOR	H
	XOR	B
	LD	(HL),A	;ECRITURE DU PATTERN CALCULE
	DEC	HL
	DEC	DE
	LD	A,D
	OR	E
TRSL4	JP	NZ,MOST5
;
;	VERIFICATION DE LA ZONE A TESTER
;
	POP	HL
	POP	DE
	PUSH 	DE
	PUSH 	HL
MOST6	LD	A,L
	XOR	H
	XOR	B
	LD	C,A
	LD	A,(HL)	;LECTURE DU PATTERN
	CP	C
	CALL	NZ,ERR?	;SI A # C
	DEC	HL
	DEC	DE
	LD	A,D
	OR	E
TRSL5	JP	NZ,MOST6
	INC	B
TRSL6	JP	NZ,MOST4
	POP	BC
	POP	DE
	RET
*EJECT
;
;
;	INITIALISATION
;
INI	LD	SP,STAK	;INIT DU STACK POINTEUR
	CALL	INIT	;INIT DU PRINTER
	XOR	A
	LD	(AUT),A	;RESET TEST AUTOMATIQUE
	LD	HL,0FFFFH
	LD	(NOBC),HL	;RESET NOMBRE DE BOUCLES
TST0	LD      HL,(NOBC)
	INC	HL	;NOMBRE DE BOUCLES + 1
	LD	(NOBC),HL
TST00	LD	DE,MES1
	CALL	AFFI
	CALL	NBC	;AFFICHAGE NUMERO DE BOUCLES
;
;	TEST MEMOIRE BANC 1
TST1	LD	HL,AB1L
	LD	DE,LB1
	LD	BC,AB1U
	CALL	MOSTEK
	LD	A,(MERR)
	OR	A
	JP	Z,TST2	;PAS D ERREUR
	LD	A,(AUT)
	OR	A
	JP	Z,TST2
	JP	TST00	;BOUCLAGE SUR BANC 1
;
;	TEST MEMOIRE BANC 2
;
TST2	LD	DE,MES2
	CALL	AFFI
	CALL	NBC
	LD	HL,AB2L
	LD	DE,LB1
	LD	BC,AB2U
	CALL	MOSTEK
	LD	A,(MERR)
	OR	A
	JP	Z,TST3	;PAS D'ERREUR
	LD	A,(AUT)
	OR	A
	JP	Z,TST3
	JP	TST2	;BOUCLAGE SUR BANC 2
;
;	TEST MEMOIRE BANC 3
;
TST3	LD	DE,MES3
	CALL	AFFI
	CALL	NBC
	LD	HL,AB3L
	LD	DE,LB1
	LD	BC,AB3U
	CALL	MOSTEK
	LD	A,(MERR)
	OR	A
	JP	Z,TST4	; PAS D'ERREUR
	LD 	A,(AUT)
	OR	A
	JP	Z,TST4
	JP	TST3	;BOUCLAGE SUR BANC 3
;
;	TEST MEMOIRE BANC 0
;
TST4	LD	DE,MES4
	CALL	AFFI
	CALL	NBC
	LD	HL,AB0L
	LD	DE,LB2
	LD	BC,AB0U
	CALL	MOSTEK
	LD	A,(AUT)
	OR	A
	JP	NZ,TST5
	LD	DE,MES5
	CALL	AFFI
	CALL	CLUB
	SUB	030H
	OR	A
	JP	Z,AUTO
	LD	A,040H
	DEFB	SIM	;PASSAGE EN MORTE
	JP	MEMO
AUTO	LD	A,1
	LD	(AUT),A
	JP	TST0
TST5	LD	A,(MERR)
	OR	A
	JP	Z,TST0
	JP	TST4	;BOUCLAGE SUR BANC 0
*EJECT
;
;	SOUS-PROGRAMMES
;	***************
;
;	SP CLAVIER
CLUB	IN	A,(ADSC)
	RRCA
	JP	NC,CLUB
	IN	A,(ADEC)
	AND	07FH
	RET
;
;	EDITION DE CARACTERE AU PRINTER
;
OUT	PUSH	HL
	LD	HL,NOMB
	DEC	(HL)
	JP	NZ,ONPE
	PUSH	BC
	CALL	INIT
	POP	BC
ONPE	POP	HL
	CALL	CO
	RET
;
;	INIT PRINTER
;
INIT	LD	C,VIPR
	LD	A,050H
	LD	(NOMB),A
	CALL	CO
ATT	IN	A,(ADSC)
	RLCA
	JP	NC,ATT
	RET
;
;	ECIRTURE SUR PRINTER
;
CO	IN	A,(ADSC)
	RLCA
	RLCA
	JP	NC,CO
	LD	A,C
	OR	080H
	OUT	(ADSC),A
	LD	A,C
	OUT	(ADSC),A
	RET
;
;	AFFICHAGE DE CARASTERE SUR AFFICHEUR
;
AFFI	LD	B,AAFF
AFFI1	LD	HL,AFFI2+1
	LD	(HL),B
	LD	A,(DE)
	CP	ETX
	RET	Z
AFFI2	OUT	(AAFF),A
	INC	DE
	DEC	B
	JP	AFFI1
;
;	CONVERSION BINAIRE ASCII
;
CONV	PUSH	HL
	PUSH	BC
	LD	HL,TC
	LD	B,A
	AND	0FH
	CALL	HLPA
	LD	E,(HL)
	LD	A,B
	AND	0F0H
	RLCA
	RLCA
	RLCA
	RLCA
	LD	HL,TC
	CALL	HLPA
	LD	D,(HL)
	POP	BC
	POP	HL
	RET
;
;	HL = HL + A
;
HLPA
	ADD	A,L
	LD	L,A
	RET 	NC
	INC	H
	RET
;
;	EDITION D'ERREUR
;
ERR?	PUSH 	DE
	PUSH 	HL
	PUSH 	BC
	LD	A,H	;@ POIDS FORTS
	CALL	CONV
	LD	C,D
	CALL	OUT
	LD	C,E
	CALL	OUT
	LD	A,L
	CALL	CONV
	LD	C,D
	CALL	OUT
	LD	C,E
	CALL	OUT
	LD	C,020H
	CALL	OUT
	POP	BC
	LD	A,C
	PUSH	BC
	CALL	CONV	;PATTERN ECRIT
	LD	C,D
	CALL	OUT
	LD	C,E
	CALL	OUT
	LD	C,020H
	CALL	OUT
	LD	A,(HL)
	CALL	CONV	;PATTERN LU
	LD	C,D
	CALL	OUT
	LD	C,E
	CALL	OUT
	LD	C,020H
	CALL	OUT
	CALL	OUT
	LD	HL,CPT
	LD	A,(HL)
	INC	(HL)
	CP	3
	JP 	NZ,ERRT
	LD	(HL),0
	LD	A,(CRLF)
	LD	C,A	;LF
	CALL	OUT
	LD	A,(CRLF+1)
	LD	C,A	;CR
	CALL	OUT
	LD	A,1
	LD	(MERR),A	;MEMORISATION D ERREUR
ERRT	POP	BC
	POP	HL
	POP	DE
	RET
;
;	AFFICHAGE PATTERN DE TEST EN COURS
;
NUPA	PUSH 	HL
	PUSH 	DE
	PUSH 	BC
	OUT	(AA2),A	;INDICTION DE LA PHASE + OU -
	LD	A,B
	CALL	CONV
	LD	A,D
	OUT	(AA1),A
	LD	A,E
	OUT	(AA0),A
	POP	BC
	POP	DE
	POP	HL
	RET
;
;	NBC : AFFICHAGE DU NOMBRE DE BOUCLES
;
NBC	EQU	$
	LD	HL,(NOBC)	;COMPTEUR DE BOUCLES
	LD	A,H
	CALL	CONV
	LD	A,D
	OUT	(ACT4),A
	LD	A,E
	OUT	(ACT3),A
	LD	A,L
	CALL	CONV
	LD	A,D
	OUT	(ACT2),A
	LD	A,E
	OUT	(ACT1),A
	RET
*EJECT
;
;
;	RESERVATIONS MEMOIRE
;
;MESSAGES
;
MES1	DEFM	'TEST MEMOIRE BANC 1             '
	DEFB	ETX
MES2	DEFM	'TEST MEMOIRE BANC 2             '
	DEFB	ETX
MES3	DEFM	'TEST MEMOIRE BANC 3             '
	DEFB	ETX
MES4	DEFM	'TEST MEMOIRE BANC 0             '
	DEFB	ETX
MES5	DEFM	'TEST AUTOMATIQUE ? (OUI=0)      '
	DEFB	ETX
CRLF	DEFB	0AH,0DH
;
AUT	DEFB	0	;FLAG DE TEST AUTOMATIQUE
;
MERR	DEFB	0	;MEMORISATION D ERREUR
;
NOMB	DEFB	0	;COMPTEUR BUFFER PRINTER
;
CPT	DEFB	0	;COMPTEUR ERREUR
;
NOBC	DEFS	2	;COMPTEUR DE BOUCLES
;
TC	DEFM	'0123456789ABCDEF'
	END	INI
