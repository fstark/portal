
;
	POP	AF
	AND	40H	;RSS. GEREE PAR NGF ?
	JP	NZ,DIRN00	;NON
;
;	FINIR DE REMPLIR 'DES'
;
	INC	HL
	LD	(HL),FLECT	;FONCTION DE LECTURE
	LD	HL,BUFF
	LD	(DES+3),HL	;ADRESSE MEMOIRE = BUFF
	LD	HL,100H		;1 SECTEUR
	LD	(DES+5),HL	;LONGUEUR = 1 SECTEUR
	LD	H,0
	LD	(DES+7),HL 	;ADR. SECTEUR POIDS FORTS = 0
;
;	AIGUILLAGE VERS VOLUME OU FICHIER
;
	POP	AF	;INDICATEUR DE RETOUR DE L'ANALYSE (ANFC
;
	JP	C,DIRV0	;DIR. VOL. SI NOM DE SUPPORT SEUL
	JP	PO,DIR11	;SUPPORT IMPLICITE
	PUSH	AF	;SAUV. Z
	LD	A,(DET)		;*  DANS NOM ?
	AND	A
	JP	NZ,DIR10	;NON
	LD	A,(DET+1)	;POSITION * DANS NOM
	SUB	4		;POSITION * DANS DNF (NOM)
	LD	(DET+1),A
	JP	Z,DIR10		;* EN TETE
	CP	6		;TEST SI * ENTRE 2 CAR.
	JP	Z,DIR10		;NON * EN FIN
	LD	HL,DNF+2
	ADD	A,L
	LD	L,A
	LD	A,H
	ADC	A,0
	LD	H,A
	INC	HL
	LD	A,(HL)
	CP	20H
	LD	A,SFI
	JP	NZ,VISER	;* ENTRE 2 CAR.  ERREUR
DIR10	POP	AF
DIR11	LD	A,SFI
	JP	NZ,VISER	;ERREUR SI PAS DE NOM DE VOLUME
	LD	A,(ZONE)
	CP	CR
	JP	NZ,DIRF	; SAUT SI COMMANDE VIDE
	JP	DIRV0	; DIRECTORY VOLUME
*EJECT
;
;************************************************
;						*
;	VERIFICATION OPTION LIS=LO		*
;	--------------------------		*
;						*
;************************************************
;
;	ENTREE	HL = ADR. COMMANDE
;
;	S
	DEFB	CR
*EJECT
;
;
;****************************************************************
;								*
;	DIRECTORY D'UN VOLUME					*
;								*
;		ENTREE	IMPP = ECRAN OU IMPRIMANTE		*
;			DES EST INITIALISE EN LECTURE		*
;								*
;****************************************************************
;
;
DIRV0	CALL	LINOMV	;LECTURE ET IMPRESSION NOM VOL.
;
;	LECTURE ET IMPRESSION LONG. GRANULE
;
	LD	HL,DES+7
	DEC	(HL)	;SECTEUR 4
	LD	HL,DES
	CALL	ES
	JP	NZ,VISER
;
	LD	A,(BUFF+2)
	CP	INDIC
	LD	A,?VNNGF	;VOL. NON CREE PAR NGF
	JP	NZ,VISER
;
	LD	HL,MGR
	CALL	SMESG
	CALL	BZONE	;METTRE A BLANC ZONE
	LD	HL,(BUFF)
	LD	A,H
	LD	(LGCAT),A	;LONG. CATALOGUE
	LD	D,0	;DE = LG. GRANULE
	LD	E,L
	DEC	E
	INC	DE	;SI 0 ALORS TRANSFORME EN 256
	LD	HL,ZONE
	CALL	BNDC	;TRANSCODAGE DECIMAL DANS ZONE
	LD	HL,ZONE
	LD	B,3	;MAX. 256
	CALL	SMESG1 ;ET SORTIR LA TAILLE DES GRANULES
;
;	LECTURE TABLE DES GRANULES
;	SORTIE NB. TOTAL DE GRANULES
;
	LD	HL,DES+7
	LD	(HL),0	;SECTEUR 0
	LD	HL,DES+4
	INC	(HL)
	LD	HL,DES
	CALL	ES
	JP	NZ,VISER
;
	LD	HL,MTAUX
	CALL	SMESG	;SORTIE MESSAGE TAUX
;
	CALL	BZONE	;METTRE ZONE A BLANC
	LD	HL,(BUF2)
	PUSH HL
	POP	BC	;BC = NB. TOTAL DE GRANULES
	EX	DE,HL	;DE = NB. TOTAL DE GRANULES
	LD	HL,ZONE
	CALL	BNDC	;TRANSCODAGE DECIMAL DS ZONE
	LD	(HL),'/'	;SEPARATEUR
	INC	HL
	PUSH	HL	;SAUV. ADR. DANS ZONE
;
;	CALCUL NB. GRANULES OCCUPES
;
;		ENTREE	BC = NB. TOTAL DE GRANULES
;
;
	LD	DE,0	;DE = COMPT. GRANULES OCCUPES
	LD	HL,BUF2+2
DIRV1	LD	A,(HL)
	PUSH	HL
	LD	H,8
;
DIRV2	RLA
	JP	NC,DIRV3
	INC	DE	;1 GRANULE OCCUPE
DIRV3	PUSH	AF
	DEC	BC	;COMP. GRAN. - 1
	LD	A,B
	OR	C
	JP	Z,DIRV5	;SAUT SI FIN TABLE DES GRANULES
	POP	AF
	DEC	H
	JP	NZ,DIRV2	;GRANULE SUIVANT SI MEME OCTET
	POP	HL	;ON PASSE A L'OCTET SUIVANT
	INC	HL	;OCTET SUIVANT
	JP	DIRV1
;
;	IMPRESSION NB. GRANULES OCCUPES
;
;		ENTREE	DE = NB. DE GRANULES OCCUPES
;
;
DIRV5	POP	AF	;PILE PROPRE
	POP	HL
	POP	HL	;HL = ADR. DS ZONE
	CALL	BNDC
	LD	HL,ZONE
	LD	B,11
	CALL	SMESG1	;SORTIE NB. GRANULES OCCUPES
	LD	HL,MSPAR
	CALL	SMESG	;MESSAGE SEPARATEUR
;
	CALL	RCLF
	CALL	RCLF
*EJECT
;
;
;	LECTURE TABLE DES NOMS DE FICHIERS
;
;		ENTREE	LGCAT = LG. CATALOGUE
;			BUFF CONTIEND DEJA LE SECTEUR 4
;
;
	LD	HL,DES+7
	LD	(HL),4	;INITIALISE
	LD	B,0	;B = NB. DE FICHIERS EXISTANTS
	LD	DE,BUFF	;DE = ADR. REMPLISSAGE DS BUFF
	XOR	A
	LD	(DE),A	;ANNULE DESCRIPTEUR DU VOLUME
;
DIRV10	LD	C,0	;C = ADR. DANS LE SECTEUR
	PUSH	DE
	POP	HL	;HL = ADRESSE DE VIDAGE
;
DIRV11	LD	A,(HL)
	AND	A
	JP	Z,DIRV14	;SAUT SI PAS DE FICHIER
	INC	B	;+1 DS COMPTEUR DES FICHIERS
	PUSH	BC
	LD	C,8
DIRV12	LD	A,(HL)
	LD	(DE),A	;TRANSFERT DU NOM DE FICHIER
	INC	DE
	INC	HL
	DEC	C
	JP	NZ,DIRV12
	POP	BC
;
DIRV13	LD	A,C
	ADD	A,8
	LD	C,A
	JP	NZ,DIRV11	;SAUT SI PAS LA FIN DU SECTEUR
;
;	LECTURE SECTEUR SUIVANT
;
	LD	HL,DES+7
	LD	A,(HL)
	ADD	A,9	;SAUT DES 8 SECTEURS DE DESCRIPTEUR
	LD	(HL),A
	LD	A,(LGCAT)
	CP	(HL)
	JP	Z,DIRV20	;SAUT SI FIN DU CATALOGUE
	JP	C,DIRV20	;SECURITE
	LD	HL,DES+3
	LD	(HL),E
	INC	HL
	LD	(HL),D	;ADR. MEMOIRE PREMIER OCTET LIBRE
	LD	HL,DES
	CALL	ES	;ALLER LIRE
	JP	NZ,VISER
	JP	DIRV10	;ET CONTINUER
;
;	DEBRANCHEMENT SI LE FICHIER N'EXISTE PAS
;
DIRV14	LD	A,L
	ADD	A,8
	LD	L,A
	LD	A,H
	ADC	A,0
	LD	H,A	;HL = ADR. FICHIER SUIVANT
	JP	DIRV13	;ET CONTINUER
*EJECT
;
;
;****************************************************************
;								*
;	TRI DES NOMS DE FICHIERS				*
;								*
;		ENTREE	B   = NB. DE FICHIERS			*
;			BUFF CONTIEND LA LISTE DES NOMS		*
;								*
;		SORTIE						*
;			BC NON TOUCHES				*
;								*
;****************************************************************
;
;
DIRV20
;
	LD	A,B
	AND	A
	JP	Z,IMPN30	;PAS DE FICHIER
;
	PUSH	BC	;SAUV. NB. DE FICHIERS
	LD	HL,BUFF-8	;HL = PREMIER NOM
;
DIRV21	DEC	B
	JP	Z,DIRV30	;FIN DU TRI
	CALL	HLP8	;HL = HL + 8
;
;	RECHERCHE DU PLUS PETIT DANS LA LISTE DES NOMS RESTANTS
;
;		ENTREE	HL = ADR. DU PREMIER NOM NON TRIE
;			B  = NB. DE FICHIERS RESTANT DERRIERE
;
	PUSH	BC
	PUSH	HL	