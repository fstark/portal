,(DE)	;A = NUM. RESSOURCE
	LD	(HL),A
	PUSH	AF	;SAUV. NUM. RESS.
	INC	HL
	INC	DE
	LD	A,(DE)
	LD	(HL),A	;NUMERO D'UNITE
;
	POP	AF
	AND	40H	;RSS. GEREE PAR NGF ?
	JP	NZ,DIRN00	;NON
;
;	FINIR DE REMPLIR 'DES'
;
	INC	HL
	LD	(HL),FLECT	;FONCTION DE LECTURE
	LD	HL,BUFF
	LD	(DES+3),HL	;ADRESSE MEMOIRE = BUFF
	LD	HL,100H		;1 SECTEUR
	LD	(DES+5),HL	;LONGUEUR = 1 SECTEUR
	LD	H,0
	LD	(DES+7),HL 	;ADR. SECTEUR POIDS FORTS = 0
;
;	AIGUILLAGE VERS VOLUME OU FICHIER
;
	POP	AF	;INDICATEUR DE RETOUR DE L'ANALYSE (ANFC
;
	JP	C,DIRV0	;DIR. VOL. SI NOM DE SUPPORT SEUL
	JP	PO,DIR11	;SUPPORT IMPLICITE
	PUSH	AF	;SAUV. Z
	LD	A,(DET)		;*  DANS NOM ?
	AND	A
	JP	NZ,DIR10	;NON
	LD	A,(DET+1)	;POSITION * DANS NOM
	SUB	4		;POSITION * DANS DNF (NOM)
	LD	(DET+1),A
	JP	Z,DIR10		;* EN TETE
	CP	6		;TEST SI * ENTRE 2 CAR.
	JP	Z,DIR10		;NON * EN FIN
	LD	HL,DNF+2
	ADD	A,L
	LD	L,A
	LD	A,H
	ADC	A,0
	LD	H,A
	INC	HL
	LD	A,(HL)
	CP	20H
	LD	A,SFI
	JP	NZ,VISER	;* ENTRE 2 CAR.  ERREUR
DIR10	POP	AF
DIR11	LD	A,SFI
	JP	NZ,VISER	;ERREUR SI PAS DE NOM DE VOLUME
	LD	A,(ZONE)
	CP	CR
	JP	NZ,DIRF	; SAUT SI COMMANDE VIDE
	JP	DIRV0	; DIRECTORY VOLUME
*EJECT
;
;************************************************
;						*
;	VERIFICATION OPTION LIS=LO		*
;	--------------------------		*
;						*
;************************************************
;
;	ENTREE	HL = ADR. COMMANDE
;
;	SORTIE	Z=1 -> OPTION LIS=LO(CR OU ,) PRESENTE
;		       (IMPP) = 1
;		       HL POINTE SUR LE CR OU DERRIERE LA ,
;			A = SEPARATEUR (CR OU VIRG)
;		       DE SAUVEGARDES
;		Z=0 -> OPTION NON PRESENTE
;		       DE-HL SAUVEGARDES
;			A = 0FFH
;
LISLO
;
	PUSH	DE
	PUSH	HL
	LD	DE,MLISLO	; MESSAGE
	LD	C,6	; LONGUEUR
;
LISLO1	LD	A,(DE)
	CP	(HL)
	JP	NZ,LISLO3	; INCORRECT
	INC	DE
	INC	HL
	DEC	C
	JP	NZ,LISLO1	;CONTINUER
;
	LD	A,(HL)		;SEPARATEUR
	CP	CR
	JP	Z,LISLO2	; = CR
	CP	','
	JP	NZ,LISLO3	;PAS TROUVE SI MAUVAIS SEPAR.
	INC	HL
;
LISLO2	LD	E,A		;E = SEPARATEUR
	LD	A,1
	LD	(IMPP),A	; OPTION PRESENTE
	XOR	A		;POSITIONNER FLAG Z
	LD	A,E		;A = SEPARATEUR
	POP	DE
	POP	DE
	RET
;
LISLO3	POP	HL
	POP	DE
	LD	A,0FFH
	RET
;
MLISLO	DEFM	'LIS=LO'
	DEFB	CR
*EJECT
;
;****************************************************************
;                                                               *
;	VERIFICATION  OPTION  GR  				*
;	-------------------------                               *
;                                                               *
;****************************************************************
;
;	ENTREE :	HL  = ADR COMMANDE
;
;       SORTIE :	Z=1 ==> OPTION  GR PRESENTE
;				HL POINTE SUR LE CR OU DERRIERE ,
;				A = SEPARATEUR (CR OU VIRG)
;				(FIM) = 1
;				DE PRESERVES
;			Z=0 ==> OPTION ABSENTE
;				DE , HL PRESERVES
;				A = 0FFH
;
;
;
;
;
OPTIMP	PUSH	DE
	PUSH	HL
	LD	DE,MOPIM	;MESSAGE OPTION
	LD	C,2		;LONGUEUR
;
OPTIM1	LD	A,(DE)
	CP	(HL)
	JP	NZ,OPTIM3	;INCORRECT
	INC	DE
	INC	HL
	DEC	C
	JP	NZ,OPTIM1	;CONTINUER
;
	LD	A,(HL)		;SEPARATEUR
	CP	CR
	JP	Z,OPTIM2	; = CR
	CP	','
	JP	NZ,OPTIM3	;NOK SI MAUVAIS SEPARATEUR
	INC	HL
;
OPTIM2	LD	E,A		;E <--- SEPARATEUR
	LD	A,1
	LD	(FIM),A		;OPTION	PRESENTE
	XOR	A		;POSITIONNE FLAG Z
	LD	A,E		;A <--- SEPARATEUR
	POP	DE
	POP	DE
	RET
;
OPTIM3	POP	HL
	POP	DE
	LD	A,0FFH
	RET
;
MOPIM	DEFM	'GR'
	DEFB	CR
*EJECT
;
;
;****************************************************************
;								*
;	DIRECTORY D'UN VOLUME					*
;								*
;		ENTREE	IMPP = ECRAN OU IMPRIMANTE		*
;			DES EST INITIALISE EN LECTURE		*
;								*
;****************************************************************
;
;
DIRV0
;
;	LECTURE ET IMPRESSION DU NOM DU VOLUME
;	PUIS
;	LECTURE ET IMPRESSION DES CARACTERISTIQUES DU VOLUME
;
	CALL	EDCAR
	CALL	RCLF
	CALL	RCLF
	CALL	LTNF	;LECTURE TABLE DES NOMS FICHIERS
	JP	Z,IMPN30	;TABLE VIDE
	LD	A,(FIM)
	AND	A
	JP	NZ,EDOP		;OPTION  GR PRESENTE
	CALL	ALFTRI	;TRI ALPHABETIQUE DE LA TABLE
	JP	IMPN00	;EDITE NOMS FICHIER ET RETOUR
*EJECT
*EJECT
;
;
;	TRAITEMENTS DES SUPPORTS NON GERES PAR NGF
;
;		ENTREE  'DES' INIT. AVEC NUM. RESS. / NUM. UNITE
;			LE STACK CONTIENT LE CODE SORTIE DE ANFC
;
;
DIRN00	EQU	$
	POP	AF
	JP	C,DIRN10
	LD	A,SFI
	JP	VISER	;ERREUR SYNTAXE
;
;	IMPRESSION TITRE
;
DIRN10	LD	HL,MSUP
	CALL	SMESG	;SUPPORT
	LD	B,NBDES
	LD	HL,TPER
DIRN20	INC	HL
	INC	HL
	INC	HL
	LD	C,(HL)
	LD	A,(DES)
	CP	C
	JP	Z,DIRN30
	DEC	B
	INC	HL
	INC	HL
	INC	HL
	JP	NZ,DIRN20
	LD	HL,MBLANC	;SUPPORT INCONNU
	JP	DIRN40
;
;	TROUVE
DIRN30	DEC	HL
	DEC	HL
	EX	DE,HL
	LD	HL,ZONE+3
	LD	A,(DES+1)
	ADD	A,30H
	LD	(HL),A
	DEC	HL
	LD	A,(DE)
	LD	(HL),A
	DEC	HL
	DEC	DE
	LD	A,(DE)
	LD	(HL),A
	DEC	HL
	LD	(HL),3
;
DIRN40	CALL	SMESG	;MESSAGE : NOM DU SUPPORT
	LD	HL,MSPAR
	CALL	SMESG	;MESSAGE SEPARATEUR
	LD	HL,MSSUIT
	CALL	SMESG	;FIN MESS. SEPARATEUR
	CALL	RCLF
	CALL	RCLF
;
;
;	RECHERCHE NOMS DES FICHIERS
;
;		ENTREE	DNF  = DESCRIPTEUR POUR APPEL (INDIRECTE)
;			BUFF   RECOIT LES NOMS (MAX :255)
;
	LD	A,FDIR	;FONCT. DIRECTORY
	LD	(DES+2),A
	LD	HL,BUFF
	LD	(DES+3),HL	;ADR. 'BUFF' DANS  'DES'
	LD	HL,2040
	LD	(DES+5),HL	;LG. BUFFER = 2040 OCT.
;
	LD	HL,DES
	CALL	ES	;FONCT. DIRECTORY
	JP	Z,DIRN50	;OK
	CP	EPINF	;PERTE INFO. ?
	JP	NZ,VISER	;NON,AUTRE ERREUR
	LD	(ERREUR),A	;MEMOR. ERREUR
;
;	